template("toolchain_xcode") {
  _toolchain_cflags = ""
  if (defined(invoker.toolchain_cflags)) {
    _toolchain_cflags += string_join(" ", invoker.toolchain_cflags)
  }

  _toolchain_cflags_c = ""
  if (defined(invoker.toolchain_cflags_c)) {
    _toolchain_cflags_c += string_join(" ", invoker.toolchain_cflags_c)
  }

  _toolchain_cflags_cc = ""
  if (defined(invoker.toolchain_cflags_cc)) {
    _toolchain_cflags_cc += string_join(" ", invoker.toolchain_cflags_cc)
  }

  _toolchain_ldflags = ""
  if (defined(invoker.toolchain_ldflags)) {
    _toolchain_ldflags += string_join(" ", invoker.toolchain_ldflags)
  }

  _obj_subdir = "{{target_out_dir}}/{{label_name}}"

  toolchain(target_name) {
    tool("cc") {
      depfile = "{{output}}.d"
      command = "xcrun cc -MD -MF $depfile {{defines}} {{include_dirs}} ${_toolchain_cflags} ${_toolchain_cflags_c} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
      depsformat = "gcc"
      description = "CC {{output}}"
      outputs = [ "${_obj_subdir}/{{source_name_part}}.o" ]
    }

    tool("cxx") {
      depfile = "{{output}}.d"
      command = "xcrun c++ -MD -MF $depfile {{defines}} {{include_dirs}} ${_toolchain_cflags} ${_toolchain_cflags_cc} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
      depsformat = "gcc"
      description = "CXX {{output}}"
      outputs = [ "${_obj_subdir}/{{source_name_part}}.o" ]
    }

    tool("objc") {
      depfile = "{{output}}.d"
      command = "xcrun cc -MD -MF $depfile {{defines}} {{include_dirs}} ${_toolchain_cflags} ${_toolchain_cflags_c} {{cflags}} {{cflags_objc}} -c {{source}} -o {{output}}"
      depsformat = "gcc"
      description = "CC {{output}}"
      outputs = [ "${_obj_subdir}/{{source_name_part}}.o" ]
    }

    tool("asm") {
      depfile = "{{output}}.d"
      command = "xcrun cc -MD -MF $depfile {{defines}} {{include_dirs}} ${_toolchain_cflags} {{asmflags}} -c {{source}} -o {{output}}"
      depsformat = "gcc"
      description = "ASM {{output}}"
      outputs = [ "${_obj_subdir}/{{source_name_part}}.o" ]
    }

    tool("alink") {
      rspfile = "{{output}}.rsp"
      rspfile_content = "{{inputs}}"
      command = "rm -f {{output}} && xcrun libtool -static -o {{output}} \"@$rspfile\""
      default_output_dir = _obj_subdir
      default_output_extension = ".a"
      description = "AR {{output}}"
      outputs = [ "{{output_dir}}/{{target_output_name}}{{output_extension}}" ]
    }

    tool("solink") {
      rspfile = "{{output}}.rsp"
      rspfile_content = "{{inputs_newline}}"
      command = "rm -f {{output}} && xcrun c++ -shared ${_toolchain_cflags} ${_toolchain_ldflags} {{ldflags}} -o {{output}} -Wl,-filelist,\"$rspfile\" {{libs}} {{frameworks}}"
      default_output_dir = _obj_subdir
      default_output_extension = ".dylib"
      description = "SO {{output}}"
      outputs = [ "{{output_dir}}/{{target_output_name}}{{output_extension}}" ]
    }

    tool("link") {
      rspfile = "{{output}}.rsp"
      rspfile_content = "{{inputs_newline}}"
      command = "rm -f {{output}} && xcrun c++ ${_toolchain_cflags} ${_toolchain_ldflags} {{ldflags}} -o {{output}} -Wl,-filelist,\"$rspfile\" {{libs}} {{frameworks}}"
      default_output_dir = _obj_subdir
      description = "LINK {{output}}"
      outputs = [ "{{output_dir}}/{{target_output_name}}{{output_extension}}" ]
    }

    tool("stamp") {
      description = "STAMP {{output}}"
      command = "touch {{output}}"
    }

    tool("copy") {
      description = "COPY {{source}} {{output}}"
      command = "ln -f {{source}} {{output}} 2>/dev/null || (rm -rf {{output}} && cp -af {{source}} {{output}})"
    }
  }
}
